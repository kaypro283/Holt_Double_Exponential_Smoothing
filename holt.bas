10 CLEAR
20 REM CHRISTOPHER D. VAN DER KAAY, PH.D. 2025
30 REM HOLT'S EXPONENTIAL SMOOTHING TOOL
40 REM FOR FORECASTING FUTURE DATA FROM HISTORICALS
50 REM PROVIDES MEAN SQUARE ERROR, MEAN ABSOLUTE PERCENTAGE ERROR, 
60 REM MEAN ABSOLUTE ERROR, AND ROOT MEAN SQUARE ERROR
70 PRINT
80 PRINT  "********************************************************"
90 PRINT  "*     WELCOME TO HOLT'S LINEAR TREND EXPONENTIAL       *"
100 PRINT "*                 SMOOTHING PROGRAM                    *"
110 PRINT "*                                                      *"
120 PRINT "*       THIS PROGRAM USES HOLT'S METHOD TO FORECAST    *"
130 PRINT "*       FUTURE DATA POINTS BASED ON HISTORICAL DATA.   *"
140 PRINT "*                                                      *"
150 PRINT "*       IT CALCULATES AND PROVIDES THE FOLLOWING:      *"
160 PRINT "*       - MEAN SQUARE ERROR (MSE)                      *"
170 PRINT "*       - MEAN ABSOLUTE PERCENTAGE ERROR (MAPE)        *"
180 PRINT "*       - MEAN ABSOLUTE ERROR (MAE)                    *"
190 PRINT "*       - ROOT MEAN SQUARE ERROR (RMSE)                *"
200 PRINT "*                                                      *"
210 PRINT "*       NOTE: THE PROGRAM MAY TAKE SEVERAL MINUTES     *"
220 PRINT "*       TO RUN, DEPENDING ON THE SIZE OF THE HISTORICAL*"
230 PRINT "*       DATA SET AND FORECAST HORIZON.                 *"
240 PRINT "********************************************************"
250 PRINT
260 PRINT "PRESS ENTER TO CONTINUE..."
270 INPUT DUMMY$
280 DIM Y(200), F(200), S(200), B(200)
290 INPUT "ENTER NUMBER OF HISTORICAL DATA POINTS (1-200): "; N
300 IF N <= 0 OR N > 200 THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 290
310 FOR I = 1 TO N
320 INPUT "ENTER DATA POINT: "; A$
330 IF A$ = "" THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 320
340 Y(I) = VAL(A$)
350 NEXT I
360 INPUT "ENTER FORECAST HORIZON: "; H
370 H = INT(H)
380 IF H <= 0 THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 360
390 PRINT
400 PRINT "********************************************************"
410 PRINT "*                ALPHA AND BETA SELECTION              *"
420 PRINT "********************************************************"
430 PRINT "THE PROGRAM WILL NOW TEST DIFFERENT ALPHA AND BETA      "
440 PRINT "VALUES AND PROVIDE YOU WITH THE BEST COMBINATION OF     "
450 PRINT "ALPHA AND BETA BASED ON THE MEAN SQUARE ERROR. IT USES  "
460 PRINT "A NESTED LOOP TO TEST COMBINATIONS OF ALPHA AND BETA,   "
470 PRINT "WHICH CAN BE COMPUTATIONALLY EXPENSIVE. DEPENDING ON    "
480 PRINT "THE STEP SIZE AND RANGE FOR ALPHA AND BETA, THIS CAN    "
490 PRINT "LEAD TO MANY ITERATIONS AND MAY TAKE A FEW MINUTES TO   "
500 PRINT "RUN.                                                    "
510 PRINT 
520 PRINT "PRESS ENTER TO CONTINUE..."
530 INPUT DUMMY$
540 BESTMSE = 1.701412E+38
550 PRINT " ALPHA |  BETA  |        MSE"
560 PRINT "----------------------------"
570 FOR ALPHA = .1 TO .99 STEP .02
580 PRINT CHR$(7): PRINT "TESTING ALPHA ="; ALPHA; "..."
590 FOR BETA = .1 TO .99 STEP .02
600 PRINT "  TESTING BETA ="; BETA; "..."
610 S(1) = Y(1)
620 B(1) = Y(2) - Y(1)
630 F(1) = Y(1)
640 FOR I = 2 TO N
650 S(I) = ALPHA * Y(I) + (1 - ALPHA) * (S(I-1) + B(I-1))
660 B(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * B(I-1)
670 F(I) = S(I) + B(I)
680 NEXT I
690 MSE = 0
700 MAPE = 0
710 COUNT = 0
720 FOR I = 2 TO N
730 IF Y(I) = 0 OR F(I) = 0 THEN GOTO 760
740 MAPE = MAPE + ABS((Y(I) - F(I)) / Y(I))
750 COUNT = COUNT + 1
760 MSE = MSE + (Y(I) - F(I))^2
770 NEXT I
780 IF COUNT > 0 THEN MAPE = MAPE / COUNT * 100 ELSE MAPE = 0
790 IF MSE >= BESTMSE THEN GOTO 830
800 BESTMSE = MSE
810 BESTALPHA = ALPHA
820 BESTBETA = BETA
830 PRINT " "; ALPHA; "  | "; BETA; "  | "; MSE
840 NEXT BETA
850 PRINT "----------------------------"
860 PRINT "CURRENT BEST: ALPHA = "; BESTALPHA; ", BETA = "; BESTBETA; ", MSE = "; BESTMSE
870 PRINT
880 NEXT ALPHA
890 PRINT
900 PRINT "********************************************************"
910 PRINT "*             BEST ALPHA AND BETA SELECTON             *"
920 PRINT "********************************************************"
930 PRINT "BEST ALPHA: "; BESTALPHA
940 PRINT "BEST BETA: "; BESTBETA
950 PRINT
960 PRINT "PRESS ENTER TO VIEW ERROR/PERFORMANCE METRICS..."
970 INPUT DUMMY$
980 S(1) = Y(1)
990 B(1) = Y(2) - Y(1)
1000 F(1) = Y(1)
1010 FOR I = 2 TO N
1020 S(I) = BESTALPHA * Y(I) + (1 - BESTALPHA) * (S(I-1) + B(I-1))
1030 B(I) = BESTBETA * (S(I) - S(I-1)) + (1 - BESTBETA) * B(I-1)
1040 F(I) = S(I) + B(I)
1050 NEXT I
1060 MSE = 0
1070 MAPE = 0
1080 MAE = 0
1090 COUNT = 0
1100 FOR I = 2 TO N
1110 IF Y(I) = 0 OR F(I) = 0 THEN GOTO 1150
1120 MAPE = MAPE + ABS((Y(I) - F(I)) / Y(I))
1130 COUNT = COUNT + 1
1140 MSE = MSE + (Y(I) - F(I))^2
1150 MAE = MAE + ABS(Y(I) - F(I))
1160 NEXT I
1170 IF COUNT > 0 THEN MAPE = MAPE / COUNT * 100 ELSE MAPE = 0
1180 IF COUNT > 0 THEN MAE = MAE / COUNT ELSE MAE = 0
1190 IF COUNT > 1 THEN MSE = MSE / (COUNT - 1) ELSE MSE = 0
1200 RMSE = SQR(MSE)
1210 FOR I = N+1 TO N+H
1220 F(I) = S(N) + B(N) * (I - N)
1230 NEXT I
1240 PRINT "********************************************************"
1250 PRINT "*                  CALCULATED RESULTS                  *"
1260 PRINT "********************************************************"
1270 PRINT "MEAN ABSOLUTE ERROR: "; MAE
1280 PRINT "  MAE IS THE AVERAGE OF ABSOLUTE DIFFERENCES"
1290 PRINT "  BETWEEN ACTUAL AND FORECASTED VALUES."
1300 PRINT "  INDICATES AVERAGE ERROR."
1310 PRINT "--------------------------------------------------------"
1320 PRINT "MEAN SQUARE ERROR: "; MSE
1330 PRINT "  MSE IS THE AVERAGE OF SQUARED DIFFERENCES."
1340 PRINT "  PENALIZES LARGER ERRORS MORE."
1350 PRINT "--------------------------------------------------------"
1360 PRINT "ROOT MEAN SQUARE ERROR: "; RMSE
1370 PRINT "  RMSE IS THE SQUARE ROOT OF MSE."
1380 PRINT "  MEASURES SPREAD OF ERRORS IN THE SAME UNITS"
1390 PRINT "  AS THE DATA."
1400 PRINT "--------------------------------------------------------"
1410 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
1420 PRINT "  MAPE IS THE AVERAGE OF ABSOLUTE PERCENTAGE ERRORS."
1430 PRINT "  NORMALIZES ERRORS AS PERCENTAGES."
1440 PRINT "--------------------------------------------------------"
1450 PRINT
1460 PRINT "PRESS ENTER TO VIEW FORECASTED VALUES..."
1470 INPUT DUMMY$
1480 PRINT "********************************************************"
1490 PRINT "*                   FORECASTED VALUES                  *"
1500 PRINT "********************************************************"
1510 Z = 1.96
1520 FOR I = N+1 TO N+H
1530 UPPER = F(I) + Z * RMSE
1540 LOWER = F(I) - Z * RMSE
1550 IF LOWER < 0 THEN LOWER = 0
1560 PRINT "PERIOD "; I-N; ": FORECAST = "; F(I); " | 95% PI: ["; LOWER; " , "; UPPER; "]"
1570 NEXT I
1580 PRINT
1590 INPUT "ENTER 1 TO VIEW RESULTS AGAIN, 2 TO RESTART, 3 TO PRINT REPORT, 4 TO QUIT: "; SELECT
1600 IF SELECT = 1 THEN GOTO 900
1610 IF SELECT = 2 THEN GOTO 10
1620 IF SELECT = 3 THEN GOSUB 1680:GOTO 1590
1630 IF SELECT = 4 THEN GOTO 1660
1640 PRINT "INVALID CHOICE. PLEASE ENTER 1, 2, 3, OR 4."
1650 GOTO 1590
1660 PRINT "GOODBYE!"
1670 END
1680 REM *** REPORT PRINT SUBROUTINE ***
1690 LPRINT "********************************************************"
1700 LPRINT "*                   CALCULATED RESULTS                 *"
1710 LPRINT "********************************************************"
1720 LPRINT "MEAN ABSOLUTE ERROR: "; MAE
1730 LPRINT "  MAE IS THE AVERAGE OF ABSOLUTE DIFFERENCES"
1740 LPRINT "  BETWEEN ACTUAL AND FORECASTED VALUES."
1750 LPRINT "  INDICATES AVERAGE ERROR."
1760 LPRINT "--------------------------------------------------------"
1770 LPRINT "MEAN SQUARE ERROR: "; MSE
1780 LPRINT "  MSE IS THE AVERAGE OF SQUARED DIFFERENCES."
1790 LPRINT "  PENALIZES LARGER ERRORS MORE."
1800 LPRINT "  USEFUL FOR IDENTIFYING MODELS THAT MINIMIZE"
1810 LPRINT "  LARGE ERRORS."
1820 LPRINT "--------------------------------------------------------"
1830 LPRINT "ROOT MEAN SQUARE ERROR: "; RMSE
1840 LPRINT "  RMSE IS THE SQUARE ROOT OF MSE."
1850 LPRINT "  MEASURES SPREAD OF ERRORS IN THE SAME UNITS"
1860 LPRINT "  AS THE DATA."
1870 LPRINT "--------------------------------------------------------"
1880 LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
1890 LPRINT "  MAPE IS THE AVERAGE OF ABSOLUTE PERCENTAGE ERRORS."
1900 LPRINT "  NORMALIZES ERRORS AS PERCENTAGES."
1910 LPRINT "  FACILITATES COMPARISONS ACROSS DIFFERENT SCALES."
1920 LPRINT "--------------------------------------------------------"
1930 LPRINT
1940 LPRINT "********************************************************"
1950 LPRINT "*                   FORECASTED VALUES                  *"
1960 LPRINT "********************************************************"
1970 Z = 1.96
1980 FOR I = N+1 TO N+H
1990 UPPER = F(I) + Z * RMSE
2000 LOWER = F(I) - Z * RMSE
2010 IF LOWER < 0 THEN LOWER = 0
2020 LPRINT "PERIOD "; I-N; ": FORECAST = "; F(I); " | 95% PI: ["; LOWER; " , "; UPPER; "]"
2030 NEXT I
2040 PRINT
2050 LPRINT "*** END OF REPORT ***
2060 PRINT
2070 PRINT "END OF REPORT"
2080 RETURN
